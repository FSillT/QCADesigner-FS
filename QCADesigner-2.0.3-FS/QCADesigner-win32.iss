; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=QCADesigner
AppVerName=QCADesigner 2.0.3
AppPublisher=University of Calgary
AppPublisherURL=qcadesigner.ca
AppSupportURL=qcadesigner.ca
AppUpdatesURL=qcadesigner.ca
DefaultDirName={pf}\QCADesigner
DefaultGroupName=QCADesigner
LicenseFile=/usr/local\share\doc\QCADesigner-2.0.3\COPYING
ChangesAssociations=yes
OutputBaseFilename=QCADesigner-2.0.3-setup

[Tasks]
; NOTE: The following entry contains English phrases ("Create a desktop icon" and "Additional icons"). You are free to translate them into another language if required.
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; Flags: unchecked

[Registry]
; This adds the GTK+ libraries to QCADesigner.exe's path
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\QCADesigner.exe"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\QCADesigner.exe"; ValueType: string; ValueData: "{app}\bin\QCADesigner.exe"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\QCADesigner.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app};{code:GetGtkPath}\bin"; Flags: uninsdeletevalue
; This adds the GTK+ libraries to graph_dialog.exe's path
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\graph_dialog.exe"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\graph_dialog.exe"; ValueType: string; ValueData: "{app}\bin\QCADesigner.exe"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\graph_dialog.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app};{code:GetGtkPath}\bin"; Flags: uninsdeletevalue
; File associations
; QCA Design
Root: HKCR; SubKey: ".qca"; Flags: uninsdeletekey
Root: HKCR; SubKey: ".qca"; ValueType: string; ValueName: ""; ValueData: "QCADesigner.File"
Root: HKCR; SubKey: "QCADesigner.File"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.File"; ValueType: string; ValueName: ""; ValueData: "QCADesigner Design"
Root: HKCR; SubKey: "QCADesigner.File"; ValueType: dword; ValueName: "BrowserFlags"; ValueData: 8; Flags: uninsdeletevalue
Root: HKCR; SubKey: "QCADesigner.File"; ValueType: dword; ValueName: "EditFlags"; ValueData: 0; Flags: uninsdeletevalue
Root: HKCR; SubKey: "QCADesigner.File\DefaultIcon"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.File\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\share\QCADesigner\pixmaps\QCADesigner.winxp.ico"; MinVersion: 0, 1
Root: HKCR; SubKey: "QCADesigner.File\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\share\QCADesigner\pixmaps\QCADesigner.win32.ico"; MinVersion: 1, 0
Root: HKCR; SubKey: "QCADesigner.File\shell"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.File\shell"; ValueType: string; ValueName: ""; ValueData: "Open"
Root: HKCR; SubKey: "QCADesigner.File\shell\Open"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.File\shell\Open"; ValueType: string; ValueName: ""; ValueData: "&Open"
Root: HKCR; SubKey: "QCADesigner.File\shell\Open\command"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.File\shell\Open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\QCADesigner.exe"" ""%1"""
; QCA Simulation Results
Root: HKCR; SubKey: ".rst"; Flags: uninsdeletekey
Root: HKCR; SubKey: ".rst"; ValueType: string; ValueName: ""; ValueData: "QCADesigner.SimResults.File"
Root: HKCR; SubKey: ".sim_output"; Flags: uninsdeletekey
Root: HKCR; SubKey: ".sim_output"; ValueType: string; ValueName: ""; ValueData: "QCADesigner.SimResults.File"
Root: HKCR; SubKey: "QCADesigner.SimResults.File"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.SimResults.File"; ValueType: string; ValueName: ""; ValueData: "QCADesigner Simulation Results"
Root: HKCR; SubKey: "QCADesigner.SimResults.File"; ValueType: dword; ValueName: "BrowserFlags"; ValueData: 8; Flags: uninsdeletevalue
Root: HKCR; SubKey: "QCADesigner.SimResults.File"; ValueType: dword; ValueName: "EditFlags"; ValueData: 0; Flags: uninsdeletevalue
Root: HKCR; SubKey: "QCADesigner.SimResults.File\DefaultIcon"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.SimResults.File\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\share\QCADesigner\pixmaps\graph_dialog.winxp.ico"; MinVersion: 0, 1
Root: HKCR; SubKey: "QCADesigner.SimResults.File\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\share\QCADesigner\pixmaps\graph_dialog.win32.ico"; MinVersion: 1, 0
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell"; ValueType: string; ValueName: ""; ValueData: "Open"
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell\Open"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell\Open"; ValueType: string; ValueName: ""; ValueData: "&Open"
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell\Open\command"; Flags: uninsdeletekey
Root: HKCR; SubKey: "QCADesigner.SimResults.File\shell\Open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\graph_dialog.exe"" ""%1"""

[Files]
Source: "/usr/local\*"; DestDir: "{app}"; Flags: recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\QCADesigner"; Filename: "{app}\bin\QCADesigner.exe"; IconFilename: "{app}\share\QCADesigner\pixmaps\QCADesigner.winxp.ico"; MinVersion: 0, 1
Name: "{group}\Simulation Results Viewer"; Filename: "{app}\bin\graph_dialog.exe"; IconFilename: "{app}\share\QCADesigner\pixmaps\graph_dialog.winxp.ico"; MinVersion: 0, 1
Name: "{userdesktop}\QCADesigner"; Filename: "{app}\bin\QCADesigner.exe"; Tasks: desktopicon; IconFilename: "{app}\share\QCADesigner\pixmaps\QCADesigner.winxp.ico"; MinVersion: 0, 1

Name: "{group}\QCADesigner"; Filename: "{app}\bin\QCADesigner.exe"; IconFilename: "{app}\share\QCADesigner\pixmaps\QCADesigner.win32.ico"; MinVersion: 1, 0
Name: "{group}\Simulation Results Viewer"; Filename: "{app}\bin\graph_dialog.exe"; IconFilename: "{app}\share\QCADesigner\pixmaps\graph_dialog.win32.ico"; MinVersion: 1, 0
Name: "{userdesktop}\QCADesigner"; Filename: "{app}\bin\QCADesigner.exe"; Tasks: desktopicon; IconFilename: "{app}\share\QCADesigner\pixmaps\QCADesigner.win32.ico"; MinVersion: 1, 0

[Code]

var
  Exists: Boolean;
  GtkPath: String;

function GetGtkInstalled (): Boolean;
begin
  Exists := RegQueryStringValue (HKLM, 'Software\GTK\2.0', 'Path', GtkPath);
  if not Exists then begin
    Exists := RegQueryStringValue (HKCU, 'Software\GTK\2.0', 'Path', GtkPath);
  end;
   Result := Exists
end;

function GetGtkPath (S: String): String;
begin
    Result := GtkPath;
end;

function InitializeSetup(): Boolean;
begin
  Result := GetGtkInstalled ();
  if not Result then begin
    MsgBox ('Please install the GTK+ Runtime Environment version 2.2 or higher before installing QCADesigner. You can obtain GTK+ from http://gaim.sourceforge.net/win32/index.php#wingtk . Look for files of the form gtk-runtime-<Version>.exe.', mbError, MB_OK);
  end;
end;
